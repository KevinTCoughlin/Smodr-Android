apply plugin: 'android'

import com.rain.utils.android.robocop.generator.*;

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath files('RoboCoP/RoboCoP-0.5-jar-with-dependencies.jar')
    }
}

repositories {
    mavenCentral()
}

configurations {
    patch
}

task contentProviderGen {
    description = 'Generating Smodr ContentProvider.'
    doLast {
        System.out.println("Generating ContentProvider...")
        String schemaFilename = 'RoboCoP/schema.json';
        String baseOutputDir = 'src/main/java/';
        ContentProviderGenerator.generateContentProvider(schemaFilename, baseOutputDir);
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }

    buildTypes {
        dev {
            applicationIdSuffix '.dev'
        }
    }
}

dependencies {
    compile 'com.google.apis:google-api-services-analytics:v3-rev96-1.18.0-rc'
    compile 'com.google.android.gms:play-services:4.4.52'
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.android.support:appcompat-v7:19.1.0'
    compile 'com.android.support:gridlayout-v7:19.1.0'
    compile 'com.android.support:mediarouter-v7:19.1.0'
    compile 'com.android.support:support-v13:19.1.0'
    //compile 'com.github.manuelpeinado.fadingactionbar:fadingactionbar:3.1.2'
    compile 'joda-time:joda-time:2.3'
    compile 'com.jakewharton:butterknife:5.1.+'
    compile 'com.jakewharton.timber:timber:2.4.+'
    compile 'com.squareup.picasso:picasso:2.3.+'
    compile 'com.squareup.retrofit:retrofit:1.6.0'
    compile ('com.squareup.retrofit:converter-simplexml:1.6.+') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    compile files('lib/okhttp-urlconnection-2.0.0.patched.jar')

    //compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'javax.annotation:jsr250-api:1.0'

    patch 'com.squareup.okio:okio:1.0.0'
    patch 'com.googlecode.jarjar:jarjar:1.3'
    patch 'com.squareup.okhttp:okhttp:2.0.0'
    patch 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
}
project.ext.set("shouldBuildPatch", { !new File('lib/okhttp-urlconnection-2.0.0.patched.jar').exists() })

task downloadPatchLibs(type: Copy) {
    into('lib')
    from(configurations.patch)
    exclude('jarjar*')
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}
downloadPatchLibs.doFirst {
    if(!shouldBuildPatch()) { throw new StopExecutionException() }
}

task applyPatch(dependsOn: 'downloadPatchLibs') << {
    if (shouldBuildPatch()) {
        project.ant {
            taskdef name: "jarjar", classname: "com.tonicsystems.jarjar.JarJarTask", classpath: configurations.patch.asPath
            jarjar(jarfile: 'lib/okhttp-urlconnection-2.0.0.patched.jar', filesetmanifest: "merge") {
                zipfileset(src: 'lib/okio-1.0.0.jar')
                zipfileset(src: 'lib/okhttp-2.0.0.jar')
                zipfileset(src: 'lib/okhttp-urlconnection-2.0.0.jar')
                rule pattern: "okio.**", result: "custom.okio.@1"
            }
        }
    }
}

task cleanupDownloadPatchLibs(type: Delete, dependsOn: 'applyPatch') {
    delete 'lib/okio-1.0.0.jar'
    delete 'lib/okhttp-2.0.0.jar'
    delete 'lib/okhttp-urlconnection-2.0.0.jar'
}

assemble.dependsOn(cleanupDownloadPatchLibs)

task cleanPatch(type: Delete) {
    delete 'lib'
}
clean.dependsOn(cleanPatch)